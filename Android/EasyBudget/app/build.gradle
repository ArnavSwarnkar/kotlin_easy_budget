/*
 *   Copyright 2022 Benoit LETONDOR
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'dagger.hilt.android.plugin'

apply from: 'batch.gradle'
apply from: 'iap.gradle'

repositories {
    google()
    maven { url "https://maven.batch.com/release" }
    mavenCentral()
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.benoitletondor.easybudgetapp"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 81
        versionName "2.4.0"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                    "room.incremental":"true"
                ]
            }
        }
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_LOG", "true"
            buildConfigField "boolean", "CRASHLYTICS_ACTIVATED", "false"
            buildConfigField "String", "BATCH_API_KEY", batch_devkey
            buildConfigField "boolean", "ANALYTICS_ACTIVATED", "false"
            buildConfigField "boolean", "DEV_PREFERENCES", "true"
            buildConfigField "String", "LICENCE_KEY", licence_key
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "DEBUG_LOG", "false"
            buildConfigField "boolean", "CRASHLYTICS_ACTIVATED", "true"
            buildConfigField "String", "BATCH_API_KEY", batch_livekey
            buildConfigField "boolean", "ANALYTICS_ACTIVATED", "true"
            buildConfigField "boolean", "DEV_PREFERENCES", "false"
            buildConfigField "String", "LICENCE_KEY", licence_key

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            storeFile file('../key/debug.jks')
            storePassword 'uFdRPMWz69R28t6m9zV53jmw9hJVK3'
            keyAlias 'easybudget'
            keyPassword 'uFdRPMWz69R28t6m9zV53jmw9hJVK3'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.work:work-gcm:2.7.1'
    implementation 'com.google.android.play:core:1.10.3'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    implementation platform("com.google.firebase:firebase-bom:29.2.1")
    implementation "com.google.firebase:firebase-messaging-ktx"
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.firebaseui:firebase-ui-auth:7.2.0'

    implementation 'com.android.billingclient:billing-ktx:4.1.0'

    implementation project(':caldroid')
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'me.relex:circleindicator:2.1.6@aar'
    implementation 'com.batch.android:batch-sdk:1.18.2'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    kapt "androidx.room:room-compiler:2.4.2"
    implementation "androidx.room:room-runtime:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"

    implementation 'net.lingala.zip4j:zip4j:2.5.2'
}

apply plugin: 'com.google.gms.google-services'
